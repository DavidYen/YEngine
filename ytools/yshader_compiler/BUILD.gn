
tool_executable("yshader_compiler") {
  sources = [
    "main.cpp",
    "compile_shader.h",
  ]

  deps = [
    "//schemas:shader_schema_cpp",
    "//third_party/build/google/gflags",
    "//third_party/build/google/flatbuffers",
    "//third_party/build/rapidjson",
    "//ytools/file_utils",
    "//ytools/report_utils",
  ]

  if (gfx == "dx9" || gfx == "d3d12" || gfx == "dx11") {
    sources += [
      "d3d_shader_compiler.cpp",
      "d3d_shader_compiler.h",
    ]

    if (gfx == "d3d12" || gfx == "dx11") {
      sources += [
        "compile_shader_d3d_shader_model5.cpp",
      ]
    } else {
      sources += [
        "compile_shader_${gfx}.cpp",
      ]
    }

    deps += [
      "//build/config/gfx/${gfx}:D3DCompiler",
    ]
  } else {
    assert(false, "No Shader compiler defined for ${gfx}.")
  }
}
