
config("schema_config") {
  include_dirs = [
    "${root_build_dir}/include",
  ]
}

template("compile_schema") {
  action(target_name) {
    schema = invoker.schema

    script = "//build/compile_flatbuffer.py"
    inputs = [
      "//schemas/${schema}.fbs",
    ]
    outputs = [
      "${root_build_dir}/include/schemas/${schema}_generated.h",
    ]

    args = [ "--depfile", rebase_path("${target_gen_dir}/${target_name}.d"),
             "--","flatc", "-c", "--scoped-enums",
             "-o", rebase_path("${root_build_dir}/include/schemas/",
                               root_build_dir),
             rebase_path("//schemas/${schema}.fbs", root_build_dir) ]

    deps = [
      "//third_party/build/google/flatbuffers:flatc"
    ]

    depfile = "${target_gen_dir}/${target_name}.d"

    all_dependent_configs = [ ":schema_config" ]
  }
}

compile_schema("compile_shader_schema") {
  schema = "shader"
}

compile_schema("compile_module_binary_schema") {
  schema = "module_binary"
}
