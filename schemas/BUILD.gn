
config("schema_config") {
  include_dirs = [
    "${root_build_dir}/include",
  ]
}

template("compile_schema") {
  action(target_name) {
    schema = invoker.schema

    script = "//build/cmd_wrapper.py"
    inputs = [
      "//schemas/${schema}.fbs",
    ]
    outputs = [
      "${root_build_dir}/include/schemas/${schema}_generated.h",
    ]

    args = [ "--capture", rebase_path("${target_gen_dir}/${target_name}.d"),
             "--filter-capture", rebase_path("${root_build_dir}/") + "=",
             "--", "flatc", "-c", "-M",
             "-o", rebase_path("${root_build_dir}/include/schemas/"),
             rebase_path("//schemas/${schema}.fbs", root_build_dir) ]

    deps = [
      "//third_party/build/google/flatbuffers:flatc"
    ]

    depfile = "${target_gen_dir}/${target_name}.d"

    all_dependent_configs = [ ":schema_config" ]
  }
}

compile_schema("compile_shader_schema") {
  schema = "shader"
}
