
config("schema_config") {
  include_dirs = [
    "${root_build_dir}/include",
  ]
}

template("compile_schema") {
  schema_target = target_name
  action("${schema_target}_cpp") {
    schema = invoker.schema

    script = "//build/compile_flatbuffer.py"
    outputs = [
      "${root_build_dir}/include/schemas/${schema}_generated.h",
    ]

    args = [ "--depfile", rebase_path("${target_gen_dir}/${schema_target}_cpp.d"),
             "--","flatc", "-c", "--scoped-enums",
             "-o", rebase_path("${root_build_dir}/include/schemas/",
                               root_build_dir),
             rebase_path("//schemas/${schema}.fbs", root_build_dir) ]

    deps = [
      "//third_party/build/google/flatbuffers:flatc"
    ]

    if (defined(invoker.deps_cpp)) {
      deps += invoker.deps_cpp
    }

    if (defined(invoker.data_deps_cpp)) {
      data_deps = invoker.data_deps_cpp
    }

    depfile = "${target_gen_dir}/${schema_target}_cpp.d"

    all_dependent_configs = [ ":schema_config" ]
  }

  action("${schema_target}_python") {
    schema = invoker.schema
    schema_class = invoker.schema_class

    script = "//build/compile_flatbuffer.py"

    # The dep format does not seem to work for python, so we should rely
    # on the C++ depfile instead. This should be ok because more things depend
    # on the C++ schema anyways.
    inputs = [
      "${root_build_dir}/include/schemas/${schema}_generated.h",
    ]

    # There is a python file produced for each table/struct, we will just rely
    # on the fact that all flatbuffers will be in the yengine_data and there
    # will be a file produced named the schema_class.
    outputs = [
      "${root_build_dir}/python/yengine_data/${schema_class}.py"
    ]

    args = [ "--","flatc", "-p",
             "-o", rebase_path("${root_build_dir}/python/",
                               root_build_dir),
             rebase_path("//schemas/${schema}.fbs", root_build_dir) ]

    deps = [
      ":${schema_target}_cpp",
      "//third_party/build/google/flatbuffers:flatc",
    ]

    data_deps = [
      "//third_party/build/google/flatbuffers:flatbuffers_python",
    ]

    all_dependent_configs = [ ":schema_config" ]
  }
}

compile_schema("binding_type_schema") {
  schema = "binding_type"
  schema_class = "BindingType"
}

compile_schema("param_type_schema") {
  schema = "param_type"
  schema_class = "ParamType"
}

compile_schema("bind_param_schema") {
  schema = "bind_param"
  schema_class = "BindParam"

  data_deps_cpp = [
    ":param_type_schema_cpp",
  ]
}

compile_schema("bind_params_schema") {
  schema = "bind_params"
  schema_class = "BindParams"

  data_deps_cpp = [
    ":bind_param_schema_cpp",
  ]
}

compile_schema("material_schema") {
  schema = "material"
  schema_class = "Material"

  data_deps_cpp = [
    ":bind_param_schema_cpp",
  ]
}

compile_schema("module_binary_types_schema") {
  schema = "module_binary_types"
  schema_class = "BinaryType"
}

compile_schema("module_binary_schema") {
  schema = "module_binary"
  schema_class = "ModuleBinary"

  data_deps_cpp = [
    ":module_binary_types_schema_cpp",
  ]
}

compile_schema("render_passes_schema") {
  schema = "render_passes"
  schema_class = "RenderPasses"
}

compile_schema("render_targets_schema") {
  schema = "render_targets"
  schema_class = "RenderTargets"
}

compile_schema("render_type_schema") {
  schema = "render_type"
  schema_class = "RenderType"
}

compile_schema("sampler_schema") {
  schema = "sampler"
  schema_class = "Sampler"
}

compile_schema("shader_schema") {
  schema = "shader"
  schema_class = "Shader"

  data_deps_cpp = [
    ":binding_type_schema_cpp",
    ":param_type_schema_cpp",
  ]
}

compile_schema("texture_schema") {
  schema = "texture"
  schema_class = "Texture"
}

compile_schema("vertex_member_types_schema") {
  schema = "vertex_member_types"
  schema_class = "VertexMemberType"
}

compile_schema("vertex_usage_types_schema") {
  schema = "vertex_usage_types"
  schema_class = "VertexUsageType"
}

compile_schema("vertex_decl_schema") {
  schema = "vertex_decl"
  schema_class = "VertexDecl"

  data_deps_cpp = [
    ":vertex_member_types_schema_cpp",
    ":vertex_usage_types_schema_cpp",
  ]
}

compile_schema("mesh_schema") {
  schema = "mesh"
  schema_class = "Mesh"

  data_deps_cpp = [
    ":vertex_member_types_schema_cpp",
    ":vertex_usage_types_schema_cpp",
  ]
}